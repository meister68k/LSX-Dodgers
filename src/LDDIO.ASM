
;	LSX-Dodgers DIO

DRDX:
	CALL	DRDY
	RET	Z
	CALL	DRDX1
	RET	C
	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	HL,(_DTBUF)
	CALL	DRDC
	CALL	C,DRDNE
DRDXE1:
	POP	DE
	POP	BC
DRDXE2:
	POP	HL
	RET

DRDN:
	XOR	A
DRDN1:	NOP
	JR	NC,DRDX
DRDNX:
	CALL	DRDY
	RET	Z
DRDX1:
	CALL	DWTX
	LD	(_DCLPS),DE
	LD	A,(_DRV)
	LD	(_DBDRV),A
	CALL	_CHGDRV
	RET	NC
DRDNE:
	SBC	A,A
	LD	(_DBDRV),A
	RET

DRDY:
	PUSH	HL
	LD	A,(_DRV)
	LD	HL,_DBDRV
	XOR	(HL)
	JR	NZ,DRDY1

	LD	HL,(_DCLPS)
	SBC	HL,DE		;CF=0
DRDY1:
	POP	HL
	RET

DWTX:
	LD	A,(_WTDBF)
	OR	A
	RET	Z
	XOR	A
	LD	(_WTDBF),A

	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	A,(_DBDRV)
	CALL	_CHGDRV
	LD	DE,(_DCLPS)
	LD	HL,(_DTBUF)
	CALL	NC,DWTC
	JR	DRDXE1

RDFATX:
	PUSH	HL
	LD	A,(_DRV)
	LD	HL,_FATDRV
	XOR	(HL)
	AND	$7F
	JR	Z,DRDY1

	PUSH	BC
	PUSH	DE
	PUSH	IX
	CALL	WTFATX
	JR	C,RDFATE1

	LD	A,(_DRV)
	LD	(_FATDRV),A
	CALL	_RDFAT
RDFATE2:
	JR	NC,RDFATE1
	SBC	A,A
	LD	(_FATDRV),A
RDFATE1:
	POP	IX
	JR	DRDXE1

WTFATX:
	LD	A,(_WTFATF)
	OR	A
	RET	Z
	PUSH	HL
	LD	A,(_FATDRV)
	LD	HL,_DBDRV
	XOR	(HL)
	AND	$7F
	CALL	Z,DWTX
	JR	C,DRDY1
	PUSH	BC
	PUSH	DE
	PUSH	IX
	CALL	WTFAT
	XOR	A
	LD	(_WTFATF),A
	JR	RDFATE1

NCL1:
	LD	A,D
	OR	E
	SCF
	RET	Z

	LD	A,(_DRV)
	BIT	3,D
	JR	Z,NCL2
	OR	$80
NCL2:
	PUSH	HL
	LD	HL,_FATDRV
	CP	(HL)
	JR	Z,DRDY1

	PUSH	BC
	PUSH	DE
	PUSH	IX
	PUSH	AF
	CALL	WTFATX
	POP	BC
	JR	C,RDFATE1
	LD	A,B
	LD	(_FATDRV),A
	CALL	RDFATS
	JR	RDFATE2

NCL3:
	RES	3,D
	LD	L,E
	LD	H,D
	SRL	H
	RR	L
	LD	BC,(_FATBF)
	RET

GNCL:
	CALL	NCL1		;GET NEW CLUSTER
	RET	C
	PUSH	BC
	PUSH	HL
	CALL	NCL3
	JR	C,GNC1
	ADD	HL,DE
	ADD	HL,BC
	LD	E,(HL)
	INC	HL
	LD	A,(HL)
	AND	$0F
	LD	D,A
	POP	HL
	POP	BC
	RET
GNC1:
	ADD	HL,DE
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	D,(HL)
	LD	B,4
GNC1L:
	SRL	D
	RRA
	DJNZ	GNC1L

	LD	E,A
	POP	HL
	POP	BC
	AND	A
	RET

SNCL:
	CALL	NCL1
	RET	C
	LD	A,1
	LD	(_WTFATF),A
;				SET NEW CLUSTER
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	HL
	CALL	NCL3
	JR	C,SNC1
	ADD	HL,DE
	ADD	HL,BC
	POP	DE
	LD	(HL),E
	INC	HL
	LD	A,D
	AND	$0F
	LD	D,A
	LD	A,(HL)
	AND	$F0
	OR	D
	LD	(HL),A
	JR	SNC2
SNC1:
	ADD	HL,DE
	ADD	HL,BC
	POP	DE
	EX	DE,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	EX	DE,HL
	LD	A,(HL)
	AND	$0F
	OR	E
	LD	(HL),A
	INC	HL
	LD	(HL),D
SNC2:
	POP	HL
	POP	DE
	POP	BC
	RET

RDFAT0X:
	CALL	RDFATS
	RET	C
	CP	(IX+1)
	RET	Z
	SCF
	RET

RDFAT:
	LD	A,(_DRV)
	CALL	CHGDRVR
	RET	C
	LD	A,(IX+$12)
	CP	$87
	JR	NZ,RDFAT0X

	LD	A,$7F
	LD	(CHECK),A	;A=$7F
	BIT	0,(IX+$0A)
	JR	Z,X2HX
E2DX:
	LD	HL,M2D
	CALL	DRDF
	JR	C,E2HX
	CP	$FD		;2D
	RET	Z
	CP	$F9		;2DD9
	JP	Z,E2DD9
	CP	$FB		;2DD8
	SCF
	RET	NZ		;NOT 2DD
	LD	HL,M2DD8
CHGTYPE:
	PUSH	DE
	CALL	CHGDV
	LD	A,(_DRIVE)
	AND	3
	OR	$80
	LD	L,A
	LD	H,_CYL0/256
	LD	A,$FF
	LD	(HL),A
	POP	DE
	RET

CHGDV:
	PUSH	IX
	POP	DE
	LDI
	LDI
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	LD	BC,12
	LDIR
CHGDV2:
	LD	A,$FF
	LD	(_DSK),A
	LD	A,(_DRV)
	JP	_CHGDRV

E2HX:
	INC	A
	RET	Z
	LD	HL,CHECK
	RLC	(HL)
	RET	C
X2HX:
	LD	HL,M2HD
	LD	DE,0
	CALL	DRDD
	JR	NC,X2HX1
	INC	A
	RET	Z
	LD	HL,CHECK
	RLC	(HL)
	RET	C
	JR	E2DX
X2HX1:
	INC	HL
	LD	A,(HL)
	LD	B,$FE
	CP	$0C
	JR	Z,X2HDE
	LD	B,$F8
	SUB	$20		;$20
	JR	Z,X2HDE
	INC	A		;$1F
	JR	Z,X2HDE
	INC	A		;$1E
	JR	NZ,C2HX		;2HD/2HC/2HQ
X2HS:
	LD	HL,M2HS
	CALL	X2HSS
	RET	C
	LD	HL,(_FATBF)
	INC	HL
	LD	A,(HL)		;FATBF+1
	CP	80
	CCF
	RET	NC
	CP	90
	RET	NC
	LD	(IX+$0C),A
	SUB	80
	ADD	A,A	;*2
	LD	C,A
	ADD	A,A	;*4
	ADD	A,A	;*8
	ADD	A,A	;*16
	ADD	A,C	;*18
	ADD	A,$98
	LD	(IX+8),A
	LD	A,5
	ADC	A,0
	LD	(IX+9),A
	JR	CHGDV2
X2HDE:
	LD	HL,M2HDE
	INC	HL
	LD	(HL),B
	DEC	HL
X2HSS:
	CALL	DRDF
	RET	C
	CP	(IX+1)
	SCF
	RET	NZ
	CALL	INCDRD
	JR	NC,DRDC
	RET

E2DD9:
	LD	HL,M2DD
	CALL	CHGTYPE
	LD	HL,(_FATBF)
INCDRD:
	INC	H
	INC	H
	INC	H
	INC	H
DRDC:
	CALL	_DRD
	RET	C
	LD	A,(DBLPAT)
	OR	A
	JP	NZ,_DRD
	RET

DWTC:
	CALL	_DWT
	RET	C
	LD	A,(DBLPAT)
	OR	A
	JP	NZ,_DWT
	RET

DRDF:
	LD	DE,(_FATPS)
DRDD:
	CALL	CHGTYPE
	LD	HL,(_FATBF)
	PUSH	HL
	CALL	DRDC
	POP	HL
	RET	C
	LD	A,(HL)
	RET

C2HX:
	CALL	RDFATS
	RET	C
	CP	$FE
	RET	Z
	CP	$F0
	JR	Z,X2HQ
	CP	$F9
	SCF
	RET	NZ
	PUSH	HL
	LD	HL,M2HC
X2HQ1:
	CALL	CHGTYPE
	POP	HL
	LD	B,4
	JR	RDFATL
X2HQ:
	PUSH	HL
	LD	HL,M2HQ
	JR	X2HQ1

RDFATS:
	CALL	FATS
	RET	C
RDFATL:
	PUSH	BC
	CALL	_DRD
	POP	BC
	RET	C
	DJNZ	RDFATL
	LD	HL,(_FATBF)
	LD	A,(HL)
	RET

WTFAT:
	CALL	FATS
	CALL	NC,WTFATL
	RET	C
	BIT	1,(IX+$0F)
	RET	Z
	CALL	FATS2
	PUSH	BC
	LD	C,(IX+0)
	LD	B,0
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
WTFATL:
	PUSH	BC
	CALL	_DWT
	POP	BC
	RET	C
	DJNZ	WTFATL
	RET

FATS:
	LD	A,(_FATDRV)
	CALL	CHGDRVR
	RET	C
FATS2:
	LD	DE,(_FATPS)
	LD	HL,(_FATBF)
	LD	B,(IX+0)
	LD	A,6
	CP	B
	RET	NC
	LD	A,(_FATDRV)
	ADD	A,A
	JR	NC,FATS3
	LD	A,B
	SUB	6
	LD	B,A
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	RET
FATS3:
	LD	B,6
	XOR	A
	RET

CHGDRV:
	AND	$7F
	PUSH	HL
	LD	HL,_DSK
	CP	(HL)
	JR	Z,CHGDRVE
CHGDRV1:
	PUSH	IX
	CALL	CHGDRV0
	LD	A,(_DSK)
	POP	IX
CHGDRVE:
	POP	HL
	RET

CHGDRV0:
	LD	L,A
	CALL	_GETDPB
	RET	C

	LD	A,L
	LD	(_DSK),A

	DI
	LD	(SPPAT2+1),SP

	LD	SP,IX

	POP	HL
	POP	HL
	LD	(_DRD+1),HL

	POP	HL
	LD	(_DWT+1),HL

	POP	HL
	LD	(CLSPAT+1),HL	;ADDCL

	POP	HL		;MAXCL
	LD	A,L
	LD	(CLPAT2+1),A
	LD	A,H
	LD	(CLPAT1+1),A
	DEC	HL
	LD	(MAXCLP+1),HL

	POP	HL		;H=MAXDIR L=FDMODE
	LD	A,L
	OR	$FE
	LD	(FDMODE+1),A
	RLCA
	AND	3
	LD	(FSPAT+1),A
	LD	A,H
	LD	(RTEPAT+1),A

	POP	HL
	LD	A,L
	LD	(_MAXCYL),A
	LD	A,H
	LD	(MAXSEC+1),A

	POP	HL
	LD	H,0
	LD	(_FATPS),HL

	POP	HL
	LD	A,H
	LD	(SECPAT+1),A
	XOR	A		;CF=0 ษ ภา
	LD	H,A
	LD	(_DIRPS),HL

	POP	HL
	LD	A,H
	LD	(_DRIVE),A

	POP	HL
	POP	HL
	LD	(_GNCL+1),HL

	POP	HL
	LD	(_SNCL+1),HL

SPPAT2:	LD	SP,0
	EI
	PUSH	DE
	BIT	7,(IX+$0F)
	JR	Z,NEC

	LD	DE,$0218
	LD	HL,0

	LD	A,3
	JR	NEC1
NEC:
	LD	DE,$38CB	;SRL B
	LD	HL,$3FCB	;SRL A

	LD	A,3
NEC1:
	LD	(IBM5+1),A	;3 1
	INC	A		;4 2
	LD	(IBM7+2),A
	ADD	A,A		;8 4
	ADD	A,A		;$10 8
	ADD	A,A		;$20 $10
	LD	(IBM1+1),A
	ADD	A,A		;$40 $20
	ADD	A,A		;$80 $40
	ADD	A,A		;$00 $80
	CPL			;$FF $7F
	LD	(IBM2+1),A
	LD	(IBM4),DE
	LD	(IBM8),HL
	POP	DE
	XOR	A
	RET

GETDPBD:
	EX	(SP),IX
	PUSH	IX
	LD	A,(_DRV)
	JR	GETDPB1

CHGDRVR:
	CALL	_CHGDRV
	RET	C
	LD	A,(_DSK)
GETDPB1:
	JP	_GETDPB

GETDPB:
	CP	8
	CCF
	RET	C
	ADD	A,A	;*2
	ADD	A,A	;*4
	ADD	A,A	;*8
	ADD	A,A	;*16
	ADD	A,A	;*32
	LD	IXL,A
	LD	IXH,DEVICE/256
	LD	A,(IX)
	CP	$01		;A=0 THEN CF=1
	RET

FFLUSH:
	PUSH	AF
	LD	A,$FF
	LD	(SFILE),A
	CALL	DWTX
	LD	A,$FF
	LD	(_DBDRV),A
	CALL	WTFATX
	LD	A,$FF
	LD	(_FATDRV),A
	POP	AF
	RET

M2D:
	DB	2,$FD
	DW	$2908,356
	DB	$FF,12,40,9,1,2,5,1

M2DD8:
	DB	2,$FB
	DW	$2908,636
	DB	$FF,12,80,8,1,2,5,1

M2DD:
	DB	3,$F9
	DW	$290A,715
	DB	$FF,14,80,9,1,2,7,1

M2HC:
	DB	7,$F9
	DW	27,2373
	DB	$FE,29,80,15,1,$82,15,1

M2HD:
	DB	2,$FE
	DW	9,1223
	DB	$FE,11,77,8,1,2,5,1

M2HQ:
	DB	9,$F0
	DW	31,2845
	DB	$FE,36,80,18,1,$82,19,1

M2HDE:
	DB	3,$FE
	DW	11,1429
	DB	$FE,13,80,9,1,2,7,1

M2HS:
	DB	3,$FB
	DW	8,1432
	DB	$FE,10,80,9,1,1,4,10

CHECK:	DB	0
